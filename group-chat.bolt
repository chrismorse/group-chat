//
// Chat Room Info
//
type RoomInfo {
  private: Boolean;
  name: String;
}

path /rooms {
  read() { true }
}

path /rooms/{rid} is RoomInfo {
  validate() { isPushID(rid) }
  create() { auth != null }
  update() { getRoomRole(rid) == 'owner' }
}

function getRoomInfo(rid) { prior(root).rooms[rid] }

function isRoomPublic(rid) { getRoomInfo(rid).private == false }

//
// Room Membership
//
type Member {
  nickname: Nickname;
  role: Role;
}

type Role extends String {
  validate() { this.test(/^owner|applicant|member|banned$/) }
}

path /members/{rid} {
  validate() { isPushID(rid) }
  read() { isRoomOwner(rid) }
  create() { getRoomInfo(rid) != null }
}

path /members/{rid}/{uid} is Member {
  validate() { isUID(uid) }
  read() { isCurrentUser(uid) }
  create() {
    getRoomInfo(rid) != null && isCurrentUser(uid) && (
      isRoomPublic(rid) && this.role == 'member' ||
      this.role == 'applicant'
    )
  }
  update() { isRoomOwner(rid) }
}

function isRoomOwner(rid) { getRoomRole(rid) == 'owner' }

function isRoomMember(rid) { getRoomRole(rid).test(/^owner|member$/) }

function getRoomRole(rid) { prior(root).members[rid][auth.uid].role }

type Nickname extends String {
  validate() { this.length >= 3 }
}

//
// Messages
//
type Message {
  from: UID;
  when: Number;
  message: String;

  validate() { this.when == now }
}

path /messages/{rid} {
  read() { isRoomPublic(rid) || isRoomMember(rid) }
  create() { isRoomMember(rid) }
  delete() { isRoomOwner(rid) }
}

//
// Authentication
//
type UID extends String {
  validate() { isUID(this) }
}

function isUID(s) { s.length >= 20 }

function isCurrentUser(uid) { auth != null && auth.uid == uid }

function isPushID(s) { s.length == 20 }
